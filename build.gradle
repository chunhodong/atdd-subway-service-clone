buildscript {
	ext {
		springBootVersion = '2.7.7'
	}
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "io.spring.gradle:dependency-management-plugin:1.0.15.RELEASE"
	}
}

allprojects {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'
	apply plugin: 'jacoco'


	group = 'chunhodong'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'

	jacoco {
		toolVersion = '0.8.5'
	}
	repositories {
		mavenCentral()
	}

	jacocoTestReport {
		dependsOn test
		reports {
			html.enabled true
			xml.enabled false
			csv.enabled false
		}
	}

	dependencies {
		// spring
		implementation 'org.springframework.boot:spring-boot-starter-web'
		// jpa
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		// handlebars
		implementation 'pl.allegro.tech.boot:handlebars-spring-boot-starter:0.3.4'
		// log
		implementation 'net.logstash.logback:logstash-logback-encoder:6.1'
		// lombok
		annotationProcessor 'org.projectlombok:lombok'
		compileOnly 'org.projectlombok:lombok:1.18.24'
		// h2
		runtimeOnly 'com.h2database:h2'
		// mysql
		implementation 'mysql:mysql-connector-java'
		// rest-assured
		testImplementation 'io.rest-assured:rest-assured:3.3.0'
		// jwt
		implementation 'io.jsonwebtoken:jjwt:0.9.1'
		// test
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'net.jqwik:jqwik:1.7.0'
	}

	tasks.named('test') {
		useJUnitPlatform()
		finalizedBy 'jacocoTestReport'
	}

}

project(':subway-member') {
	dependencies {
		implementation project(':subway-common')

		testImplementation project(':subway-common').sourceSets.test.output
	}
}

project(':subway-line') {
	dependencies {
		implementation project(':subway-common')
		implementation project(':subway-station')

		testImplementation project(':subway-common').sourceSets.test.output
		testImplementation project(':subway-station').sourceSets.test.output
	}
}

project(':subway-path') {
	dependencies {
		implementation project(':subway-common')
		implementation project(':subway-line')
		implementation project(':subway-station')
		implementation project(':subway-auth')

		testImplementation project(':subway-common').sourceSets.test.output
	}
}

project(':subway-station') {
	dependencies {
		implementation project(':subway-common')

		testImplementation project(':subway-common').sourceSets.test.output
	}
}

project(':subway-auth') {
	dependencies {
		implementation project(':subway-common')
		implementation project(':subway-member')
	}
}
